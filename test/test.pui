let
# Useful
window n s = [n,s];
column n p = [n,p];
uiPage n ui = [n,ui];

# Ontology
type = %f06efa5e-190f-4e5d-8633-495c5683c124;
name = %498260df-6a8a-44f0-b285-68a63565a33b;

People = @type !78baed51-cb05-46b5-bcb4-49031532b890;

mother = %3afce58f-b7eb-4b11-8a75-2d66afd4d085;
father = %c005705f-9259-4d24-9713-db28a6e4f7d5;
parents p = mother p | father p;
children p = @mother p | @father p;

employer = %72f612af-ae47-4c18-b3df-6932ae914f62;

email = %8649b74c-123e-4ece-be3b-b734b0db3ea7;
phone = %83dfc88b-7b5b-4262-a548-0af84d5e3b63;
home = %b4f7736e-2db4-486c-aae5-b6a9b1035fd0;
address = %fd83bef7-145c-4e40-a2d6-938bb5fb6da1;
city = %55c84133-64f2-47a6-8b3c-e01256ec9779;
postcode = %80f99100-407b-4a6b-a4f0-a025c94132b3;
province = %dffae424-28d5-4543-b1b2-b8a744bbc0cd;
country = %1257c340-0ec1-4ff7-9fd8-d2eaa7822463;
birthdate = %b711b9b3-b43c-4ff6-be12-3291cc6b050a;
deathdate = %805a9e65-d69a-4261-9c94-a3249885b6bb;

# UI
uiPeople pp = uiTable [column "Name" name] wPerson pp;
uiPerson p = uiVertical
[
    uiLabelled "Name" $ uiTextEntry $ name p,
    uiPages
    [
        uiPage "Social" $ uiVertical
        [
            uiLabelled "Email" $ uiTextEntry $ email p,
            uiLabelled "Phone" $ uiTextEntry $ phone p
        ],
        uiPage "Home" $ let homep = home p in uiVertical
        [
            uiLabelled "Address" $ uiTextArea $ address homep,
            uiLabelled "City" $ uiTextEntry $ city homep,
            uiLabelled "Postcode/ZIP" $ uiTextEntry $ postcode homep,
            uiLabelled "State/Province" $ uiTextEntry $ province homep,
            uiLabelled "Country" $ uiTextEntry $ country homep,
            uiLabelled "Phone" $ uiTextEntry $ phone homep
        ],
        uiPage "Work" $ uiVertical
        [
            uiLabelled "Employer" $ uiTextEntry $ (name . employer) p
        ],
        uiPage "Family" $ uiVertical
        [
            uiLabelled "Birth" $ uiTextEntry $ birthdate p,
            uiLabelled "Death" $ uiTextEntry $ deathdate p,
            uiLabelled "Mother" $ uiPick name People $ mother p,
            uiLabelled "Father" $ uiPick name People $ father p,
            uiPeople $ children p
        ]
    ]
];
wPerson p = window "Item" $ uiPerson p;

wPeople = window "People" $ uiPeople People;

in
[wPeople]
