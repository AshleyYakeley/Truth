# Run "pinafore-query --doc" to see all predefined bindings.

let
# Useful
window n s = [n,s];
column n p = [n,p];
ui_page n ui = [ui_label n,ui];
ui_labelled n ui = ui_horizontal [[ui_label n,false],[ui,true]];

# Ontology
type = %f06efa5e-190f-4e5d-8633-495c5683c124;
name = %498260df-6a8a-44f0-b285-68a63565a33b;

People = @type !78baed51-cb05-46b5-bcb4-49031532b890;

mother = %3afce58f-b7eb-4b11-8a75-2d66afd4d085;
father = %c005705f-9259-4d24-9713-db28a6e4f7d5;
parents p = mother p | father p;
children p = @mother p | @father p;

employer = %72f612af-ae47-4c18-b3df-6932ae914f62;

email = %8649b74c-123e-4ece-be3b-b734b0db3ea7;
phone = %83dfc88b-7b5b-4262-a548-0af84d5e3b63;
home = %b4f7736e-2db4-486c-aae5-b6a9b1035fd0;
address = %fd83bef7-145c-4e40-a2d6-938bb5fb6da1;
city = %55c84133-64f2-47a6-8b3c-e01256ec9779;
postcode = %80f99100-407b-4a6b-a4f0-a025c94132b3;
province = %dffae424-28d5-4543-b1b2-b8a744bbc0cd;
country = %1257c340-0ec1-4ff7-9fd8-d2eaa7822463;
birthdate = %b711b9b3-b43c-4ff6-be12-3291cc6b050a;
deathdate = %805a9e65-d69a-4261-9c94-a3249885b6bb;
married = %380ac576-9252-4783-b2d8-db222c683a22;

# UI
ui_people pp = ui_table [column "Name" name] window_person pp;
ui_person p = ui_vertical
[
    [ui_labelled "Name: " $ ui_textentry $ name p,false],
    [ui_pages
    [
        ui_page "Social" $ ui_vertical
        [
            [ui_labelled "Email: " $ ui_textentry $ email p,false],
            [ui_labelled "Phone: " $ ui_textentry $ phone p,false]
        ],
        ui_page "Home" $ let homep = home p in ui_vertical
        [
            [ui_labelled "Address: " $ ui_textarea $ address homep,false],
            [ui_labelled "City: " $ ui_textentry $ city homep,false],
            [ui_labelled "Postcode/ZIP: " $ ui_textentry $ postcode homep,false],
            [ui_labelled "State/Province: " $ ui_textentry $ province homep,false],
            [ui_labelled "Country: " $ ui_textentry $ country homep,false],
            [ui_labelled "Phone: " $ ui_textentry $ phone homep,false]
        ],
        ui_page "Work" $ ui_vertical
        [
            [ui_labelled "Employer: " $ ui_textentry $ (name . employer) p,false]
        ],
        ui_page "Family" $ ui_vertical
        [
            [ui_labelled "Birth: " $ ui_textentry $ birthdate p,false],
            [ui_labelled "Death: " $ ui_textentry $ deathdate p,false],
            [ui_checkbox "Married" $ married p,false],
            [ui_labelled "Mother: " $ ui_pick name People $ mother p,false],
            [ui_labelled "Father: " $ ui_pick name People $ father p,false],
            [ui_people $ children p,true]
        ]
    ],true]
];
window_person p = window (name p) $ ui_person p;

window_people = window "People" $ ui_people People;

aval = !a4706230-2c61-44b1-b600-516e7a54c607;
bval = !9d284ad2-e141-4114-ae50-8829a6db710f;
window_calc = window "Calc" $ ui_vertical
    [
        [ui_textentry aval,false],
        [ui_textentry bval,false],
        [ui_label $ aval / bval,false]
    ];

in
[window_people,window_calc]
