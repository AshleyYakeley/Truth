#!/usr/bin/pinafore

let

import UIStuff.Selection;

### EventTime

# An event can either be at a particular time, or a whole-day event.
closedtype EventTime
    = EventTimeDate Date !"Date" # all-day event
    | EventTimeTime LocalTime !"Time"; # event at a time

showEventTime: EventTime -> Text;
showEventTime et = case et of
    EventTimeDate d -> unixFormatDate "%F" d;
    EventTimeTime t -> unixFormatLocalTime "%F %R" t;
    end;

eventTimeDate: EventTime -> Date;
eventTimeDate et = case et of
    EventTimeDate d -> d;
    EventTimeTime (MkLocalTime d _) -> d
    end;

setEventTimeDate: Date -> Maybe EventTime -> Maybe EventTime;
setEventTimeDate d et = Just $ case et of
    Just (EventTimeTime (MkLocalTime _ t)) -> EventTimeTime (MkLocalTime d t);
    _ -> EventTimeDate d;
    end;

eventRefTimeDate: WholeRef EventTime -> WholeRef Date;
eventRefTimeDate r = lensMapWhole eventTimeDate setEventTimeDate r;

eventRefTimeTimeOfDay: WholeRef EventTime -> WholeRef TimeOfDay;
eventRefTimeTimeOfDay r = let
    eventTimeTimeOfDay: Maybe EventTime -> Maybe TimeOfDay;
    eventTimeTimeOfDay met = case met of
        Just (EventTimeTime (MkLocalTime _ tod)) -> Just tod;
        _ -> Nothing;
        end;

setEventTimeTimeOfDay: Maybe TimeOfDay -> Maybe EventTime -> Maybe (Maybe EventTime);
setEventTimeTimeOfDay mtod met = Just $ case met of
    Nothing -> Nothing;
    Just et -> Just $ case mtod of
        Nothing -> EventTimeDate $ eventTimeDate et;
        Just tod -> EventTimeTime $ MkLocalTime (eventTimeDate et) tod;
        end;
    end;
in maybeLensMapWhole eventTimeTimeOfDay setEventTimeTimeOfDay r;

# for ordering
eventTimeTime: EventTime -> LocalTime;
eventTimeTime et = case et of
    EventTimeDate d -> MkLocalTime d midnight;
    EventTimeTime t -> t
    end;

eventTimeOrder: RefOrder EventTime;
eventTimeOrder = mapOrder eventTimeTime localChronological;


### Event

opentype Event;
events: FiniteSetRef Event;
events = property @Event @() !"type.event" !@ {()};

eventName = property @Event @Text !"identify.name";
eventTime = property @Event @EventTime !"event.time";
eventNotes = property @Event @Text !"item.description";

eventOrder: RefOrder Event;
eventOrder = orderOn eventTime eventTimeOrder;

eventData: FiniteSetRef (EventTime,Text);
eventData = (eventTime !** eventName) !$$ events;

eventDataOrder: RefOrder (EventTime,Text);
eventDataOrder = mapOrder fst eventTimeOrder;

eventList: WholeRef +Date -> WholeRef +[(EventTime,Text)];
eventList todayref = {
    let
        today: Date;
        today = %todayref;

        pickCurrent: (EventTime,Text) -> Boolean;
        pickCurrent (t,_) = ge $ calendrical (eventTimeDate t) today;
    in (EventTimeDate today,"Today") :: filter pickCurrent %(setList eventDataOrder eventData)
    };


### UI

# Text for the overview tab.
overviewRef: WholeRef +Date -> WholeRef +Text;
overviewRef todayref = {textConcat $ mapList (\(t,n) -> showEventTime t <> ": " <> n <> "\n") %(eventList todayref)};

# Overview page
overviewPage: (UI.Element,UI.Element);
overviewPage = uiPage {"Overview"} $ UI.run $ do
    now <- newClock $ secondsToDuration 60; # update every minute
    tz <- newTimeZoneRef now;
    return $ UI.label $ overviewRef {case timeToLocal %tz %now of MkLocalTime d _ -> d end};
    end;

calendarWindow: WholeRef +Event -> Action UI.Window;
calendarWindow evt = fixAction $ \window ->
    UI.openWindow (200,200) {"date of " <> %(eventName !$% evt)} {[]} $ UI.vertical
    [
        (False,UI.calendar $ eventRefTimeDate $ eventTime !$% evt),
        (False,UI.button {"Done: " <> toText %(eventRefTimeDate $ eventTime !$% evt)} {UI.closeWindow window})
    ];

eventUI: WholeRef +Event -> UI.Element;
eventUI evt = UI.vertical
[
    (False, UI.textEntry $ eventName !$% evt),
    (False, UI.horizontal
    [
        (False, UI.button {"cal"} {calendarWindow evt}),
        (False, UI.textEntry $ interpretDateAsText $ eventRefTimeDate $ eventTime !$% evt),
        (False, UI.textEntry $ unixInterpretTimeOfDayAsText "%H:%M" $ eventRefTimeTimeOfDay $ eventTime !$% evt)
    ]),
    (True, UI.textArea $ eventNotes !$% evt)
];

eventWindow: Event -> Action UI.Window;
eventWindow evt = stdWindow (eventName !$ {evt}) {[]} $ eventUI {evt};

# Menu item for creating a new event, and telling the owning element to set its selection to it.
newEventMenuItem: (Event -> Action ()) -> UI.MenuItem;
newEventMenuItem setsel = UI.menuAction ("New Event") (Just "Ctrl+K")
    {do
    item <- newOpenEntity @Event;
    day <- getDate;
    eventTime !$ {item} := EventTimeDate day;
    eventName !$ {item} := "";
    events += item;
    setsel item;
    end};

eventsWindow: Action UI.Window;
eventsWindow = selectionWindow "Events" events [newEventMenuItem] eventWindow $ \selection -> let
    eventsTableUI: UI.Element;
    eventsTableUI = let
        columns: [(WholeRef +Text, Event -> WholeRef Text)];
        columns = let
            timeColumn: (WholeRef +Text, Event -> WholeRef Text);
            timeColumn = ({"When"},\e -> {showEventTime $ %(eventTime !$ {e})});
            nameColumn: (WholeRef +Text, Event -> WholeRef Text);
            nameColumn = ({"What"},\e -> eventName !$ {e});
            in [timeColumn,nameColumn];
        in UI.run $ do
            eventList <- getSetList eventOrder events;
            return $ UI.listTable columns eventList eventWindow (Just selection);
            end;

    eventsPage: (UI.Element,UI.Element);
    eventsPage = uiPage {"Events"} $ UI.scrolled $ UI.horizontal [(False,eventsTableUI),(True,eventUI $ immutWhole selection)];

    in UI.notebook unknown [overviewPage, eventsPage];

in eventsWindow
