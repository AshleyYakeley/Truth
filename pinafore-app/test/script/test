#!/usr/bin/pinafore

let

### Useful
ui_page n ui = (UI.label n,ui);
ui_labelled n ui = UI.horizontal [(False, UI.label n),(True, ui)];

std_window : WholeRef +Text -> UI.Element -> Action UI.Window;
std_window title contents = fixAction $ \window -> let
    menus : [UI.MenuItem];
    menus =
        [
            UI.menuSubmenu "File"
            [
                UI.menuAction "Close" (Just "Ctrl+W") {UI.closeWindow window},
                UI.menuSeparator,
                UI.menuAction "Exit" (Just "Ctrl+Q") {UI.exitUI}
            ],
            UI.menuSubmenu "Edit"
            [
                UI.menuAction "Undo" (Just "Ctrl+Z") {queueUndo},
                UI.menuAction "Redo" (Just "Ctrl+Y") {queueRedo}
            ]
        ]
    in UI.openWindow (300,400) title {menus} $ UI.scrolled contents;

### People

# Ontology

opentype Type;
opentype Person;

type = property @Person @Type !"f06efa5e-190f-4e5d-8633-495c5683c124";
name = property @Person @Text !"498260df-6a8a-44f0-b285-68a63565a33b";

people = type !@ {openEntity @Type !"78baed51-cb05-46b5-bcb4-49031532b890"};

mother = property @Person @Person !"3afce58f-b7eb-4b11-8a75-2d66afd4d085";
father = property @Person @Person !"c005705f-9259-4d24-9713-db28a6e4f7d5";
# parents : WholeRef Person -> FiniteSetRef Person;
# parents p = (mother !$ p) <|> (father !$ p);
children : WholeRef Person -> FiniteSetRef Person;
children p = mother !@ p <:|:> father !@ p;

employer = property @Person @Person !"72f612af-ae47-4c18-b3df-6932ae914f62";

email = property @Person @Text !"8649b74c-123e-4ece-be3b-b734b0db3ea7";

phone = property @Person @Text !"83dfc88b-7b5b-4262-a548-0af84d5e3b63";

opentype Location;
home = property @Person @Location !"b4f7736e-2db4-486c-aae5-b6a9b1035fd0";
address = property @Location @Text !"fd83bef7-145c-4e40-a2d6-938bb5fb6da1";
locphone = property @Location @Text !"0689535c-f220-47a2-8fcb-4782574e0749";
city = property @Location @Text !"55c84133-64f2-47a6-8b3c-e01256ec9779";
postcode = property @Location @Text !"80f99100-407b-4a6b-a4f0-a025c94132b3";
province = property @Location @Text !"dffae424-28d5-4543-b1b2-b8a744bbc0cd";
country = property @Location @Text !"1257c340-0ec1-4ff7-9fd8-d2eaa7822463";

birthdate = property @Person @Text !"b711b9b3-b43c-4ff6-be12-3291cc6b050a";
deathdate = property @Person @Text !"805a9e65-d69a-4261-9c94-a3249885b6bb";
married = property @Person @Boolean !"380ac576-9252-4783-b2d8-db222c683a22";

# UI
ui_people : FiniteSetRef Person -> UI.Element;
ui_people pp = UI.run $ do
    selectedperson <- newMemWhole;
    pplist <- orderFiniteSet noOrder pp;
    return $ UI.vertical
    [
        (False, UI.button {"New Person"} {do p <- newOpenEntity @Person; pp += p; window_person p end}),
        (False, UI.button {"View Person"} {get selectedperson >>= window_person}),
        (False, UI.button {"Remove Person"} {get selectedperson >>= \p -> pp -= p}),
        (True, UI.listTable [({"Name"},\p -> name !$ {p})] pplist window_person (Just selectedperson))
    ];
    end;

peopleByName : WholeRef +[(Person,Text)];
peopleByName = members (mapRefOrder fst $ refOrderOn name alphabetical) $ (identity !** name) !$$ people;

ui_person : WholeRef Person -> UI.Element;
ui_person p = UI.vertical
[
    (False, ui_labelled {"Name: "} $ UI.textEntry $ name !$ p),
    (True, UI.notebook unknown
    [
        ui_page {"Social"} $ UI.vertical
        [
            (False, ui_labelled {"Email: "} $ UI.textEntry $ email !$ p),
            (False, ui_labelled {"Phone: "} $ UI.textEntry $ phone !$ p)
        ],
        ui_page {"Home"} $ let homep = home !$ p in UI.vertical
        [
            (False, ui_labelled {"Address: "} $ UI.textArea $ address !$ homep),
            (False, ui_labelled {"City: "} $ UI.textEntry $ city !$ homep),
            (False, ui_labelled {"Postcode/ZIP: "} $ UI.textEntry $ postcode !$ homep),
            (False, ui_labelled {"State/Province: "} $ UI.textEntry $ province !$ homep),
            (False, ui_labelled {"Country: "} $ UI.textEntry $ country !$ homep),
            (False, ui_labelled {"Phone: "} $ UI.textEntry $ locphone !$ homep)
        ],
        ui_page {"Work"} $ UI.vertical
        [
            (False, ui_labelled {"Employer: "} $ UI.textEntry $ (name !. employer) !$ p)
        ],
        ui_page {"Family"} $ UI.vertical
        [
            (False, ui_labelled {"Birth: "} $ UI.textEntry $ birthdate !$ p),
            (False, ui_labelled {"Death: "} $ UI.textEntry $ deathdate !$ p),
            (False, UI.checkBox {"Married"} $ married !$ p),
            (False, ui_labelled {"Mother: "} $ UI.pick peopleByName $ mother !$ p),
            (False, ui_labelled {"Father: "} $ UI.pick peopleByName $ father !$ p),
            (True, ui_people (children p))
        ]
    ])
];

window_person : Person -> Action UI.Window;
window_person p = std_window (name !$ {p}) (ui_person {p});

window_people : Action UI.Window;
window_people = std_window {"People"} (ui_people people);

### Calculator

window_calc : Action UI.Window;
window_calc = let
opentype P;
pvalue = property @P @Number !"pvalue";
aval = pvalue !$ {openEntity @P !"a4706230-2c61-44b1-b600-516e7a54c607"};
bval = pvalue !$ {openEntity @P !"9d284ad2-e141-4114-ae50-8829a6db710f"};
in std_window {"Calc"} $ UI.vertical
[
    #(False, UI.textEntry aval),
    #(False, UI.textEntry bval),
    #(False, UI.label $ aval / bval)
];

### Switching Value

window_switch : Action UI.Window;
window_switch = let
opentype P;
pbool = property @P @Boolean !"pbool";
ptext = property @P @Text !"ptext";
sw_pick = pbool !$ {openEntity @P !"59eb2d87-f815-47ef-b7dc-8fe434151bb6"};
sw_val1 = ptext !$ {openEntity @P !"6d9f7afa-1091-49cc-8646-8e613bb75ee5"};
sw_val2 = ptext !$ {openEntity @P !"934712ae-73a7-4084-8a43-d618c737fd56"};
in std_window {"Switch"} $ UI.vertical
[
    #(False, UI.textEntry sw_pick),
    (False, UI.checkBox {"pick"} sw_pick),
    (True, UI.dynamic {if %sw_pick then UI.textArea sw_val1 else UI.textArea sw_val2})
];

### Imported Files

window_files : Action UI.Window;
window_files = let
#Files = @type !f649e7d7-7e98-4c86-919c-34f52ac12c7a;
#caption = %4a8ca041-a67d-4a1e-b212-9243fca635b0;
window_file f = std_window {"File"} $ UI.vertical
[
    #(False, UI.textEntry $ caption f)
];
in std_window {"Files"} $ UI.vertical
[
    #(False, UI.button "Import" {file_import Files $ \f -> std_window $ window_file f}),
    #(True, UI.listTable [column "Caption" caption, column "Size" file_size] window_file Files)
];

### Constant Edit

window_constant : Action UI.Window;
window_constant = std_window {"Constant"} $ UI.vertical
[
    (False, UI.checkBox {"off"} {False}),
    (False, UI.checkBox {"on"} {True}),
    (False, UI.textEntry {"constant"}),
    (True, UI.textArea {"constant"})
];

### CSS
window_css: Action UI.Window;
window_css = do
    cssref <- newMemWhole;
    cssref := ".y, .y > text {color:green; font-family: \"serif\";}";
    sometext <- newMemWhole;
    sometext := "whatever";
    std_window {"CSS"} $ UI.styleSheet (Debug.UI.ignoreUpdateUIExceptions cssref) $ UI.name "root" $ UI.vertical
        [
            (False, UI.styleClass "x" $ UI.name "A" $ UI.checkBox {"Box"} {True}),
            (False, UI.styleClass "x" $ UI.name "B" $ UI.button {"Button"} {return ()}),
            (False, UI.styleClass "x" $ UI.name "C" $ UI.textEntry sometext),
            (True, UI.styleClass "y" $ UI.name "D" $ UI.textArea cssref),
            (False, UI.styleClass "y" $ UI.name "E" $ UI.label {"Some Text"})
        ];
    end;

### Start Window

window_start : Action UI.Window;
window_start = do
    now <- newClock $ secondsToDuration 1;
    timeZone <- newTimeZoneRef now;
    let
        utcDate = {%now};
        localNow = {timeToLocal %timeZone %now};
        today = {case %localNow of MkLocalTime day _ -> day end};
        in do
        std_window {"Pinafore"} $ UI.vertical
        [
            (False, UI.label {"UTC time: " <> toText %now}),
            (False, UI.label {"UTC date: " <> toText %utcDate}),
            (False, UI.label {"Local time: " <> toText %localNow}),
            (False, UI.label {"Local date: " <> toText %today}),
            (False, UI.label {"Time zone offset minutes: " <> toText %timeZone}),
            (False, UI.button {"People"} {window_people}),
            (False, UI.button {"Calculated Form"} {window_calc}),
            (False, UI.button {"Switch"} {window_switch}),
            (False, UI.button {"Files"} {window_files}),
            (False, UI.button {"Constant"} {window_constant}),
            (False, UI.button {"CSS"} {window_css}),
            (False, UI.button {"Disabled"} unknown),
            (True, UI.blank)
        ];
        end;
    end;

in window_start
