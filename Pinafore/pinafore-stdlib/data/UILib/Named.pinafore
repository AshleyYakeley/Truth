import "pinafore-gnome", "UILib/Context", "UILib/Pane" in
with GTK in
namespace UILib of

    #| An open entity type; something with a name
    opentype Named;

    namespace Named of

        #| "name" property for a given store, etc.
        datatype Props of
            Mk of
                #| A name is a text that identifies (in some sense) an entity.
                nameOf: Property Named Text;
                #| Order alphabetically (case-insensitive) by name
                nameOrder: ModelOrder Named;
                #| Order a set of entities by their names.
                byName: FiniteSetModel {+a,+Named} -> WholeModel +(List (a *: Text));
            end;
        end;

        mkProps: Store -> Props
        = fn store => let
            nameOf = property @Named @Text !"identify.name" store;
            nameOrder = on.ModelOrder nameOf alphabetical.Order;
            byName = fn model => toList.FiniteSetModel (map.ModelOrder fst.Product nameOrder) $ (id.Property **.Property nameOf) !$$ model;
            in Mk.Props;

        datatype Context <: Context.Pane & Props of
            Mk of
                Mk.Context.Pane;
                Mk.Props;

                #| A pane for a widget, where the title is the name of the widget.
                namedPane: (a -> Widget) -> (a & Named) -> Pane;

                #| A "name" column for `listTable.Widget`.
                nameColumn: WholeModel +Text *: (Named -> WholeModel Text);

                #| A `SetWidget` for this set that's a list of widgets by name.
                namedTable: FiniteSetModel {a,+Named} -> (a -> Widget) -> SetWidget {a,-Named};
            end;
        end;

        mkContext: Context.UILib -> Context.Named
        = fn Mk.Context.UILib => let
            Mk.Context.Pane = mkContext.Pane Mk.Context.UILib;

            Mk.Props = mkProps store;

            namedPane = fn itemWidget, e => let
                titleModel= nameOf !$% {e};
                extraMenusModel = {[]};
                widget = itemWidget e;
                in Mk.Pane;

            nameColumn = ({"Name"},fn p => nameOf !$ {p});

            namedTable = fn sm, itemWidget => Mk.SetWidget $ fn msel => exec.Widget $ do
                lm <- getList.FiniteSetModel nameOrder sm;
                pure.Action $ listTable.Widget [nameColumn] lm (fn e => paneWindow $ namedPane itemWidget e) msel;
                end;

        in Mk.Context.Named;

    end

end;
