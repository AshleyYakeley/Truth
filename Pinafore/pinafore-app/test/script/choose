#!/usr/bin/pinafore
import "media", "gnome" in
run.GTK $ fn gtk =>
let rec

    showFile: Action Unit
    = do
        file <- chooseExistingFile.GTK gtk $ match
            Just ("image",_) => True;
            _ => False;
            end;
        ref <- makeRef.File.GIO file;
        media <- get ref;
        jpeg <- orStop $ decode jpegMedia.JPEG.Image media;
        let
        path = file >- match
                Path.File.GIO path => path;
                _ => "unknown";
            end;
        image = interpret jpeg;
        in open.Window.GTK gtk (size.Image image) {path} $ image.Widget.GTK {image};
        showFile;
    end;

in showFile
