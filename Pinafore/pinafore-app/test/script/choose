#!/usr/bin/pinafore
let
import "pinafore-media";
import "pinafore-gnome";
in
GTK.run $ fn gtk => let

rec
    showFile: Action Unit
    = do
        file <- GTK.chooseExistingFile gtk $ match
            Just ("image",_) => True;
            _ => False;
            end;
        ref <- GIO.fileMakeRef file;
        lit <- get ref;
        let
        path = file >- match
                GIO.FileParseName path => path;
                _ => "unknown";
            end;
        in lit >- match
            image : Image.JPEGImage => GTK.openWindow gtk (Image.imageSize image) {path} $ GTK.image {image};
            _ => Env.outputLn "not a JPEG image";
        end;
        showFile;
    end;
end;

in showFile
