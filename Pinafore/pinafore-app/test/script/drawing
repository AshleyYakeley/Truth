#!/usr/bin/pinafore
let
import Colour;
import Drawing;

# transform from (-1 .. +1,-1 .. +1)
scaleDrawing: WholeRef Drawing -> WholeRef (Integer :*: Integer -> Drawing);
scaleDrawing rd = {\(w,h) => let
    size = min w h;
    in translate (w / 2, h / 2) $ scale (size / 2, size / 2) %rd};

datatype Progress of
    ProgressNone;
    ProgressPartial;
    ProgressDone;
end;

drawing: WholeRef Progress -> WholeRef Drawing;
drawing ref =
{
    scale (0.8,0.8) $ rotate (3/4 ~* pi) $ lineWidth 0.1
    [
        source navy $ case %(toMaybeWhole ref) of
            Nothing =>
            [
                onClick $ ref := ProgressPartial
            ];
            Just ProgressNone =>
            [
                onClick $ ref := ProgressPartial
            ];
            Just ProgressPartial =>
            [
                fill [arc (0,0) 1 0 pi, closePath],
                onClick $ ref := ProgressDone
            ];
            Just ProgressDone =>
            [
                fill [arc (0,0) 1 0 (2 ~* pi)],
                onClick $ ref := ProgressNone
            ];
        end,
        stroke
        [
            arc (0,0) 1 0 (2 ~* pi),
            moveTo (-1,0),
            lineTo (1,0)
        ]
    ]
};

import GTK();
in GTK.run $ \gtk => do
    ref <- newMemWhole;
    GTK.openWindow gtk (300,400) {"Drawing"} {[]} $ GTK.vertical
    [
        GTK.layoutGrow $ GTK.draw $ scaleDrawing $ drawing ref,
        GTK.layoutGrow $ GTK.draw $ scaleDrawing $ drawing ref
    ];
    end
