#!/usr/bin/pinafore
let
import "media" end;

entitytype World;
world = cell.Store @World !"pinafore-notes.world" ?store;

let
    entitytype ItemType;
    entitytype ItemValue;
    datatype storable Item of
        Mk ItemType ItemValue !"pinafore-notes.Item";
    end;
    namespace Item of
        new: ItemType -> Action Item =
            fn t => map.Action (Mk t) $ new.OpenEntity. @ItemValue;
        is: ItemType -> Item -> Boolean =
            fn t, Mk it _ => it == t;
    end;
in expose Item, ItemType, new.Item, is.Item;

items: FiniteSetModel Item =
    property @Item @World !"pinafore-notes.Item-World" ?store !@ world;

nameOf: Property Item Text =
    property @Item @Text !"pinafore-notes.name" ?store;

namespace Note of
    itype: ItemType = point.OpenEntity. @ItemType !"pinafore-notes.Note";
end;
predicatetype storable Note <: Item = is.Item itype.Note;
namespace Note of
    new: Action Note = map.Action (coerce @Note) $ new.Item itype.Note;
end;

notes: FiniteSetModel Note = dynamic.Prism @Note !$$ items;
content = property @Note @CommonMarkText !"pinafore-notes.content" ?store;

import "gnome", "UILib" end;
with UILib end;

newNote: Command (Context.Command *: SetContext Note) =
    Mk.Command of
        name = "New Note";
        defaultKeyBinding = Just "Ctrl+K";
        action =
            fn (_,Mk.SetContext) =>
            do
                item <- new.Note;
                addItem item;
            end;
    end;

notePane: Note -> Pane =
    fn note =>
    let
        noteContent: WholeModel CommonMarkText =
            content !$ {note};
        textModel: TextModel =
            fromWhole.TextModel $ asText.CommonMarkText !$ noteContent;
        htmlErr: Text -> HTMLText =
            fn t => with HTMLText in tag "em" $ plain "error: " <> plain t;
        htmlRef: WholeModel +HTMLText =
            {from.Result htmlErr id $ toHTML.CommonMarkText %noteContent};
    in
    Mk.Pane of
        titleModel = {"Note"};
        contents = with Widget.GTK. in
            notebook unknown.WholeModel
            [
                (label {"Edit"},scrolled $ textView textModel),
                (label {"Show"},webView.WebKit htmlRef)
            ]
    end;

datatype Column -p of
    Mk of
        name: WholeModel +Text;
        contents: p -> WholeModel +Text;
    end;
end;

attributeColumn: Text -> Attribute -p +Text -> Column p =
    fn name, prop =>
        Mk.Column of
            name = {name};
            contents = fn item => immut.WholeModel $ prop !$ {item};
        end;

datatype TableSetWidget item of
    Mk of
        order: ModelOrder item;
        itemSet: FiniteSetModel item;
        columns: List (Column item);
        itemPane: item -> Pane;
    end;
end;

subtype TableSetWidget {a,+Entity} <: SetWidget {a,-Entity} =
    fn Mk.TableSetWidget => Mk.SetWidget $
        fn mSelectionModel =>
            exec.Widget.GTK $
            do
                itemList <- getList.FiniteSetModel order itemSet;
                let
                    columnPairs = map.List (fn Mk.Column => (name,contents)) columns;
                end;
                pure $ listTable.Widget.GTK. columnPairs itemList (fn item => paneWindow.Pane $ itemPane item) mSelectionModel;
            end;

notesPresentation: SetPresentation Note =
    Mk.SetPresentation of
        setName = "Notes";
        menuEntries = [newNote];
        itemSet = notes;
        itemPane = notePane;
        setWidget =
            Mk.TableSetWidget of
                order = on.ModelOrder nameOf order.Text;
                columns =
                [
                    attributeColumn "Name" nameOf
                ];
                itemSet = notes;
                itemPane = notePane;
            end;
    end;

in

run.Context $ fn Mk.Context =>
imply
    ?store = store;
    ?undoHandler = undoHandler;
    ?gtk = gtk;
in
do
    pane <- present.SetPresentation notesPresentation;
    _ <- paneWindow.Pane pane;
    pure ()
end
