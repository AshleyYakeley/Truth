#!/usr/bin/pinafore

let

datatype StopwatchState of
    StoppedState Duration;
    RunningState Time;
end;

toggleStopwatch: WholeRef +Time -> WholeRef StopwatchState -> Action Unit;
toggleStopwatch now r = do
    n <- get now;
    oldstate <- get r;
    newstate <- return $ case oldstate of
        StoppedState d => RunningState $ addTime (negateDuration d) n;
        RunningState t => StoppedState $ diffTime n t
        end;
    r := newstate;
    end;

showStopwatch: StopwatchState -> Time -> Text;
showStopwatch state n = case state of
    StoppedState d => "[" <> show d <> "]";
    RunningState t => show $ diffTime n t
    end;

ui: WholeRef +Time -> WholeRef StopwatchState -> GTK.Element;
ui now r = GTK.button {showStopwatch %r %now} {toggleStopwatch now r};

in do
    now <- newClock $ Seconds 0.001;
    r <- newMemWhole;
    r := StoppedState zeroDuration;
    GTK.run $ \gtk => GTK.openWindow gtk (150,40) {"Stopwatch"} $ ui now r;
    end
