#!/usr/bin/pinafore

let

datatype StopwatchState of
    StoppedState Duration;
    RunningState Time;
end;

toggleStopwatch: WholeRef +Time -> WholeRef StopwatchState -> Action Unit
= fns now r => do
    n <- get now;
    oldstate <- get r;
    newstate <- return $ oldstate >- match
        StoppedState d => RunningState $ addTime (negateDuration d) n;
        RunningState t => StoppedState $ diffTime n t
        end;
    r := newstate;
    end;

showStopwatch: StopwatchState -> Time -> Text
= fns state n => state >- match
    StoppedState d => "[" <> show d <> "]";
    RunningState t => show $ diffTime n t
    end;

ui: WholeRef +Time -> WholeRef StopwatchState -> GTK.Element
= fns now r => GTK.button {showStopwatch %r %now} {toggleStopwatch now r};

in do
    now <- newClock $ Seconds 0.001;
    r <- newMemWhole;
    r := StoppedState zeroDuration;
    GTK.run $ fn gtk => GTK.openWindow gtk (150,40) {"Stopwatch"} $ ui now r;
    end
