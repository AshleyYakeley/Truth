#!/usr/bin/env pinafore1
import "gnome"
with GTK.
let {
    datatype State {
        Stopped Duration;
        Running Time;
    };

    toggleStopwatch: WholeModel +Time -> WholeModel State -> Action Unit =
        fn now, r =>
        do {
            n <- get now;
            oldstate <- get r;
            newstate = oldstate >- fn {
                    Stopped.State d => Running.State $ negate.Duration d +.Time n;
                    Running.State t => Stopped.State $ n -.Time t
                };
            r := newstate;
        };

    showStopwatch: State -> Time -> Text =
        fn state, n =>
        state >-
        fn {
            Stopped.State d => "[" <>.Text show d <>.Text "]";
            Running.State t => show $ n -.Time t
        };

    ui: WholeModel +Time -> WholeModel State -> Widget =
        fn now, r =>
        button.Widget ap{showStopwatch %r %now} ap{toggleStopwatch now r};
} do {
    now <- newClock.Time $ Seconds 0.001;
    r <- newMem.WholeModel;
    r := Stopped.State zero.Duration;
    run $
        fn gtk =>
        open.Window gtk (150,40) ap{"Stopwatch"} $ ui now r;
}
