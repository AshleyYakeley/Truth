#!/usr/bin/pinafore
let
import "pinafore-gnome";

datatype StopwatchState of
    StoppedState Duration;
    RunningState Time;
end;

toggleStopwatch: WholeModel +Time -> WholeModel StopwatchState -> Action Unit
= fns now r => do
    n <- get now;
    oldstate <- get r;
    newstate <- return $ oldstate >- match
        StoppedState d => RunningState $ addTime (negateDuration d) n;
        RunningState t => StoppedState $ diffTime n t
        end;
    r := newstate;
    end;

showStopwatch: StopwatchState -> Time -> Text
= fns state n => state >- match
    StoppedState d => "[" <> show d <> "]";
    RunningState t => show $ diffTime n t
    end;

ui: WholeModel +Time -> WholeModel StopwatchState -> Element.GTK
= fns now r => button.GTK {showStopwatch %r %now} {toggleStopwatch now r};

in do
    now <- newClock $ Seconds 0.001;
    r <- newMem.WholeModel;
    r := StoppedState zeroDuration;
    run.GTK $ fn gtk => open.Window.GTK gtk (150,40) {"Stopwatch"} $ ui now r;
    end
