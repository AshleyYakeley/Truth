#!/usr/bin/pinafore
openDefaultStore.Env >>= fn store =>
import "pinafore-gnome" in
with GTK. in
let

    dateNotesOf: Property Date Text =
    property @Date @Text !"example.calendar:dateNotes" store;

    ui: WholeModel Date -> Element =
    fn r =>
    vertical.Element
    [
    horizontal.Element
        [
        button.Element {"<"} {do d <- get r; r := pred.Date d; end},
        layoutGrow.Element $ label.Element {encode (unixAsText.Date "%A, %B %e, %Y") %r},
        button.Element {">"} {do d <- get r; r := succ.Date d; end}
        ],
    layoutGrow.Element $ textArea.Element (fromWhole.TextModel $ dateNotesOf !$ r)
    ];

in
do
    r <- newMem.WholeModel;
    d <- getNowLocal.Date;
    r := d;
    run $ fn gtk => open.Window gtk (300,400) {show %r} $ ui r;
end
