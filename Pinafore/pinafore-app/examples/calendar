#!/usr/bin/pinafore
openDefaultStore.Env >>= fn store =>
import "pinafore-gnome" in
let

    dateText: Property Date Text
    = property @Date @Text !"example.calendar:dateText" store;

    ui: WholeModel Date -> Element.GTK
    = fn r => vertical.GTK
        [
        horizontal.GTK
            [
            button.GTK {"<"} {do d <- get r; r := pred.Date d; end},
            layoutGrow.GTK $ label.GTK {encode (unixAsText.Date "%A, %B %e, %Y") %r},
            button.GTK {">"} {do d <- get r; r := succ.Date d; end}
            ],
        layoutGrow.GTK $ textArea.GTK (fromWhole.TextModel $ dateText !$ r)
        ];

in do
    r <- newMem.WholeModel;
    d <- getNowLocal.Date;
    r := d;
    run.GTK $ fn gtk => open.Window.GTK gtk (300,400) {show %r} $ ui r;
    end
