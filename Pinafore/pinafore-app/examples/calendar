#!/usr/bin/pinafore
let

import GTK();

dateText: Date ~> Text
= property @Date @Text !"example.calendar:dateText";

ui: WholeModel Date -> GTK.Element
= fn r => GTK.vertical
    [
    GTK.horizontal
        [
        GTK.button {"<"} {do d <- get r; r := addDays (-1) d; end},
        GTK.layoutGrow $ GTK.label {unixFormatDate "%A, %B %e, %Y" %r},
        GTK.button {">"} {do d <- get r; r := addDays 1 d; end}
        ],
    GTK.layoutGrow $ GTK.textArea (wholeModelText $ dateText !$ r)
    ];

in do
    r <- newMemWholeModel;
    d <- getDate;
    r := d;
    GTK.run $ fn gtk => GTK.openWindow gtk (300,400) {show %r} $ ui r;
    end
