#!/usr/bin/pinafore
openDefaultStore.Env >>= fn store =>
let
import "pinafore-gnome";
using Storage;

dateText: Property Date Text
= property @Date @Text !"example.calendar:dateText" store;

ui: WholeModel Date -> Element.GTK
= fn r => vertical.GTK
    [
    horizontal.GTK
        [
        button.GTK {"<"} {do d <- get r; r := addDays (-1) d; end},
        layoutGrow.GTK $ label.GTK {unixFormatDate "%A, %B %e, %Y" %r},
        button.GTK {">"} {do d <- get r; r := addDays 1 d; end}
        ],
    layoutGrow.GTK $ textArea.GTK (wholeModelText $ dateText !$ r)
    ];

in do
    r <- newMemWholeModel;
    d <- getDate;
    r := d;
    run.GTK $ fn gtk => openWindow.GTK gtk (300,400) {show %r} $ ui r;
    end
