#!/usr/bin/pinafore
let

import GTK();

dateText: Date ~> Text;
dateText = property @Date @Text !"example.calendar:dateText";

ui: WholeRef Date -> GTK.Element;
ui r = GTK.vertical
    [
    GTK.horizontal
        [
        GTK.button {"<"} {do d <- get r; r := addDays (-1) d; end},
        GTK.layoutGrow $ GTK.label {unixFormatDate "%A, %B %e, %Y" %r},
        GTK.button {">"} {do d <- get r; r := addDays 1 d; end}
        ],
    GTK.layoutGrow $ GTK.textArea (dateText !$ r) unknown
    ];

in do
    r <- newMemWhole;
    d <- getDate;
    r := d;
    GTK.openWindow (300,400) {show %r} {[]} $ ui r;
    end
