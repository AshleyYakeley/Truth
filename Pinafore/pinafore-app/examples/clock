#!/usr/bin/pinafore
let
import GTK;
import Drawing;
import Colour;

scaleDrawing: Drawing -> Integer :*: Integer -> Drawing;
scaleDrawing d (w,h) = let
    size = min w h;
    in translate (w/2,h/2) $ scale (size/2,size/2) d;

circle: Number :*: Number -> Number -> Path;
circle centre radius = arc centre radius 0 (2 ~* pi);

tickMark: Integer -> Drawing;
tickMark m = let
    major = mod m 5 == 0
    in lineWidth (if major then 0.015 else 0.005) $ stroke [moveTo (0,if major then 0.8 else 0.85), lineTo (0,0.95)];

face: Drawing;
face =
[
    source antiquewhite $ fill $ circle (0,0) 1,
    source black $ lineWidth 0.01 $ stroke $ circle (0,0) 1,
    mapList (\m => rotate (m ~* pi ~/ 30) $ tickMark m) $ range 0 59
];

hourHand: Drawing;
hourHand = source navy $ lineWidth 0.1 $ stroke [moveTo (0,0), lineTo (0,-0.5)];

minuteHand: Drawing;
minuteHand = source navy $ lineWidth 0.05 $ stroke [moveTo (0,0), lineTo (0,-0.8)];

secondHand: Drawing;
secondHand = source red $ lineWidth 0.01 $ stroke [moveTo (0,0), lineTo (0,-0.9)];

in do
    now <- newClock $ Seconds 0.1;
    tz <- getCurrentTimeZone;
    GTK.run $ \gtk => openWindow gtk (300,400) {"Clock"} {[]} $ draw
    {
        scaleDrawing $
            case timeToLocal tz %now of
                DateAndTime _ (SinceMidnight (Seconds ss)) => let
                    s = floor $ modR ss 60; # floor for ticking motion
                    m = modR (ss / 60) 60;
                    h = modR (ss / 3600) 24;
                    in
                    [
                        face,
                        rotate (h ~* pi ~/ 6) hourHand,
                        rotate (m ~* pi ~/ 30) minuteHand,
                        rotate (s ~* pi ~/ 30) secondHand
                    ]
            end
    }
    end
