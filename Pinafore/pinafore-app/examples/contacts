#!/usr/bin/pinafore

let
import UI();
import UIStuff.Selection;
import UIStuff.Named;

# something with a lifespan
opentype Lifespanned;
# birth and death dates
birthdate = property @Lifespanned @Date !"lifespan.start.date";
deathdate = property @Lifespanned @Date !"lifespan.end.date";

# physical locations, that have addresses
opentype Location;
address = property @Location @Text !"location.address.withincity";
locphone = property @Location @Text !"location.telephone";
city = property @Location @Text !"location.address.city";
postcode = property @Location @Text !"location.address.postcode";
province = property @Location @Text !"location.address.province";
country = property @Location @Text !"location.address.country";

locationUI: WholeRef +Location -> UI.Element;
locationUI loc = UI.vertical
[
    UI.layoutGrow $ uiLabelled {"Address: "} $ UI.textArea $ address !$% loc,
    uiLabelled {"City: "} $ UI.textEntry $ city !$% loc,
    uiLabelled {"Postcode/ZIP: "} $ UI.textEntry $ postcode !$% loc,
    uiLabelled {"State/Province: "} $ UI.textEntry $ province !$% loc,
    uiLabelled {"Country: "} $ UI.textEntry $ country !$% loc,
    uiLabelled {"Phone: "} $ UI.textEntry $ locphone !$% loc
];

rec
# a person or organisation
dynamictype LegalEntity = Person | Organisation;
subtype LegalEntity <: Named;
subtype LegalEntity <: Lifespanned;
email = property @LegalEntity @Text !"contact.email";
phone = property @LegalEntity @Text !"contact.telephone";
home = property @LegalEntity @Location !"being.location";
legalentities: FiniteSetRef LegalEntity;
legalentities = property @LegalEntity @Unit !"type.legalentity" !@ {()};

# a person
dynamictype Person = !"type.person";
people: FiniteSetRef Person;
people = maybeMapFiniteSet (check @Person) legalentities;
# family relationships between people
mother = property @Person @Person !"relation.family.mother";
father = property @Person @Person !"relation.family.father";
children: WholeRef +Person -> FiniteSetRef Person;
children p = (mother !@% p) <:|:> (father !@% p);
married = property @Person @Boolean !"relation.family.spouse.exist";

# employment relationship
employer = property @Person @LegalEntity !"relation.employer";
employees: WholeRef +LegalEntity -> FiniteSetRef Person;
employees p = employer !@% p;

# an organisation
dynamictype Organisation = !"type.organisation";
organisations: FiniteSetRef Organisation;
organisations = property @Organisation @Unit !"type.organisation" !@ {()};
end;

### User Interface

rec
contactPages: WholeRef +LegalEntity -> List (UI.Element :*: UI.Element);
contactPages er =
[
    uiPage {"Social"} $ UI.vertical
    [
        uiLabelled {"Email: "} $ UI.textEntry $ email !$% er,
        uiLabelled {"Phone: "} $ UI.textEntry $ phone !$% er
    ],
    uiPage {"Home"} $ locationUI $ home !$% er
];

personPages: WholeRef +Person -> List (UI.Element :*: UI.Element);
personPages p =
[
    uiPage {"Work"} $ UI.vertical
    [
        uiLabelled {"Employer: "} $ UI.pick (byName legalentities) $ employer !$% p
    ],
    uiPage {"Family"} $ familyUI p
];

familyUI: WholeRef +Person -> UI.Element;
familyUI p = UI.vertical
[
    uiLabelled {"Birth: "} $ UI.textEntry $ interpretDateAsText $ birthdate !$% p,
    uiLabelled {"Death: "} $ UI.textEntry $ interpretDateAsText $ deathdate !$% p,
    UI.checkBox {"Married"} $ married !$% p,
    uiLabelled {"Mother: "} $ UI.pick (byName people) $ mother !$% p,
    uiLabelled {"Father: "} $ UI.pick (byName people) $ father !$% p,
    UI.label {"Children:"},
    UI.layoutGrow $ namedItemTable (children p) contactRefUI Nothing
];

organisationPages: WholeRef +Organisation -> List (UI.Element :*: UI.Element);
organisationPages subj =
[
    uiPage {"Employees"} $ namedItemTable (employees subj) contactRefUI Nothing
];

contactUI: LegalEntity -> UI.Element;
contactUI e = let
    pages: List (UI.Element :*: UI.Element);
    pages = contactPages {e} ++ case e of
        (p: Person) -> personPages {p};
        (o: Organisation) -> organisationPages {o};
        end;
    in UI.vertical
    [
        uiLabelled {"Name: "} $ UI.textEntry $ name !$% {e},
        UI.layoutGrow $ UI.notebook unknown pages
    ];

contactRefUI: WholeRef +LegalEntity -> UI.Element;
contactRefUI ref = UI.dynamic $ coMapWhole contactUI ref;
end;

contactsPane: Action Pane;
contactsPane = let
    menuItems =
    [
        newItemMenuAction "New Person" (Just "Ctrl+K") $ newDynamicEntity @Person,
        newItemMenuAction "New Organisation" Nothing $ newDynamicEntity @Organisation
    ];
    in namedPairPane "Contacts" legalentities menuItems contactRefUI;

contactsWindow: Action UI.Window;
contactsWindow = do
    pane <- contactsPane;
    paneWindow pane;
    end;

in contactsWindow
