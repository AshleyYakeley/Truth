#!/usr/bin/pinafore
import "pinafore-gnome", "UILib" in
with GTK., UILib. in
run.Context $ fn context =>
let

    Mk.Context.Named = mkContext.Named context;

    # something with a lifespan
    opentype Lifespanned;
    # birth and death dates
    birthdate = property @Lifespanned @Date !"lifespan.start.date" store;
    deathdate = property @Lifespanned @Date !"lifespan.end.date" store;

    # physical locations, that have addresses
    opentype Location;
    address = property @Location @Text !"location.address.withincity" store;
    locphone = property @Location @Text !"location.telephone" store;
    city = property @Location @Text !"location.address.city" store;
    postcode = property @Location @Text !"location.address.postcode" store;
    province = property @Location @Text !"location.address.province" store;
    country = property @Location @Text !"location.address.country" store;

    locationUI: WholeModel +Location -> Element
    = fn loc => vertical.Element
    [
        layoutGrow.Element $ labelled.Element {"Address: "} $ textArea.Element (fromWhole.TextModel $ address !$% loc),
        labelled.Element {"City: "} $ textEntry.Element $ city !$% loc,
        labelled.Element {"Postcode/ZIP: "} $ textEntry.Element $ postcode !$% loc,
        labelled.Element {"State/Province: "} $ textEntry.Element $ province !$% loc,
        labelled.Element {"Country: "} $ textEntry.Element $ country !$% loc,
        labelled.Element {"Phone: "} $ textEntry.Element $ locphone !$% loc
    ];

    let rec
        # a person or organisation
        dynamictype LegalEntity = Person | Organisation;
        subtype LegalEntity <: Named;
        subtype LegalEntity <: Lifespanned;
        email = property @LegalEntity @Text !"contact.email" store;
        phone = property @LegalEntity @Text !"contact.telephone" store;
        home = property @LegalEntity @Location !"being.location" store;
        legalentities: FiniteSetModel LegalEntity
        = property @LegalEntity @Unit !"type.legalentity" store !@ {()};

        # a person
        dynamictype Person = !"type.person";
        people: FiniteSetModel Person
        = maybeMap.FiniteSetModel (check @Person) legalentities;
        # family relationships between people
        mother = property @Person @Person !"relation.family.mother" store;
        father = property @Person @Person !"relation.family.father" store;
        children: WholeModel +Person -> FiniteSetModel Person
        = fn p => (mother !@% p) <:|:> (father !@% p);
        married = property @Person @Boolean !"relation.family.spouse.exist" store;

        # employment relationship
        employer = property @Person @LegalEntity !"relation.employer" store;
        employees: WholeModel +LegalEntity -> FiniteSetModel Person
        = fn p => employer !@% p;

        # an organisation
        dynamictype Organisation = !"type.organisation";
        organisations: FiniteSetModel Organisation
        = property @Organisation @Unit !"type.organisation" store !@ {()};
    end;

    ### User Interface

    let rec
        contactPages: WholeModel +LegalEntity -> List (Element *: Element)
        = fn er =>
        [
            page.Element {"Social"} $ vertical.Element
            [
                labelled.Element {"Email: "} $ textEntry.Element $ email !$% er,
                labelled.Element {"Phone: "} $ textEntry.Element $ phone !$% er
            ],
            page.Element {"Home"} $ locationUI $ home !$% er
        ];

        personPages: WholeModel +Person -> List (Element *: Element)
        = fn p =>
        [
            page.Element {"Work"} $ vertical.Element
            [
                labelled.Element {"Employer: "} $ pick.Element (byName legalentities) $ employer !$% p
            ],
            page.Element {"Family"} $ familyUI p
        ];

        familyUI: WholeModel +Person -> Element
        = fn p => vertical.Element
        [
            labelled.Element {"Birth: "} $ textEntry.Element $ reverse.Prism asText.Date !$ birthdate !$% p,
            labelled.Element {"Death: "} $ textEntry.Element $ reverse.Prism asText.Date !$ deathdate !$% p,
            checkBox.Element {"Married"} $ married !$% p,
            labelled.Element {"Mother: "} $ pick.Element (byName people) $ mother !$% p,
            labelled.Element {"Father: "} $ pick.Element (byName people) $ father !$% p,
            label.Element {"Children:"},
            layoutGrow.Element $ namedItemTable (children p) contactModelUI Nothing
        ];

        organisationPages: WholeModel +Organisation -> List (Element *: Element)
        = fn subj =>
        [
            page.Element {"Employees"} $ namedItemTable (employees subj) contactModelUI Nothing
        ];

        contactUI: LegalEntity -> Element
        = fn e => let
            pages: List (Element *: Element)
            = contactPages {e} <>.List (e >- match
                p:? Person => personPages {p};
                o:? Organisation => organisationPages {o};
                end);
            in vertical.Element
            [
                labelled.Element {"Name: "} $ textEntry.Element $ name !$% {e},
                layoutGrow.Element $ notebook.Element unknown pages
            ];

        contactModelUI: WholeModel +LegalEntity -> Element
        = fn model => dynamic.Element $ coMap.WholeModel contactUI model;
    end;

    contactsPresentation: SetPresentation LegalEntity
    = let
        title = "Contacts";
        itemSet = legalentities;
        newItems =
        [
            let name = "New Person"; mkey = Just "Ctrl+K"; newItem = new.DynamicEntity @Person in Mk.NewItem,
            let name = "New Organisation"; newItem = new.DynamicEntity @Organisation in Mk.NewItem
        ];
        itemElement = contactModelUI;
        in namedToTypePresentation Mk.NamedTypePresentation;

    contactsWindow: Action Window
    = do
        p <- presentList.Pane contactsPresentation;
        paneWindow p;
        end;

in contactsWindow
