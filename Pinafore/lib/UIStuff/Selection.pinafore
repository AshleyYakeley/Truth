let

import GTK;

### Useful GTK stuff

uiPage: WholeRef +Text -> Element -> Element :*: Element;
uiPage n ui = (label n,ui);

uiLabelled: WholeRef +Text -> Element -> Element;
uiLabelled n ui = horizontal [label n, layoutGrow ui];

#| A window that comes with some menus.
stdWindow: Context -> WholeRef +Text -> WholeRef +(List MenuItem) -> (WholeRef (Action TextRef) -> Element) -> Action Window;
stdWindow gtk title moremenus contents = fixAction $ \window => do
    textsel <- newMemWhole;
    let
        getTextRef: Action (WholeRef Text);
        getTextRef = do
            atref <- get textsel;
            atref;
        end;
        selCopy: Action Unit;
        selCopy = do
            tref <- getTextRef;
            text <- get tref;
            clipboard gtk := text;
        end;
        selCut: Action Unit;
        selCut = do
            tref <- getTextRef;
            text <- get tref;
            clipboard gtk := text;
            tref := "";
        end;
        selPaste: Action Unit;
        selPaste = do
            tref <- getTextRef;
            clip <- get $ clipboard gtk;
            case clip of
                text: Text => tref := text;
                _ => return ();
            end;
        end;
        mbar: Element;
        mbar = dynamic
            {menuBar $ [
                menuSubmenu "File"
                [
                    menuAction "Close" (Just "Ctrl+W") {closeWindow window},
                    menuSeparator,
                    menuAction "Exit" (Just "Ctrl+Q") {exit gtk}
                ],
                menuSubmenu "Edit"
                [
                    menuAction "Undo" (Just "Ctrl+Z") {queueUndo},
                    menuAction "Redo" (Just "Ctrl+Y") {queueRedo},
                    menuSeparator,
                    menuAction "Cut" (Just "Ctrl+X") {selCut},
                    menuAction "Copy" (Just "Ctrl+C") {selCopy},
                    menuAction "Paste" (Just "Ctrl+V") {selPaste}
                ]
            ] ++ %moremenus};
        in openWindow gtk (600,800) title $ vertical [mbar, layoutGrow $ scrolled $ contents textsel];
    end;

#| A pane is a title, GTK element, and menu.
datatype Pane of
    MkPane (WholeRef Text) (WholeRef (Action TextRef) -> Element) (WholeRef (List MenuItem))
end;

paneWindow: Context -> Pane -> Action Window;
paneWindow gtk (MkPane title ui moremenus) = stdWindow gtk title moremenus ui;

pane: Text -> (WholeRef (Action TextRef) -> Element) -> Pane;
pane title ui = MkPane {title} ui {[]};

#| A notebook from a list of panes
notebookPane: WholeRef +Text -> List Pane -> Action Pane;
notebookPane title panes = do
    tabRef <- newMemWhole;
    return $ let
        makePage: WholeRef (Action TextRef) -> Pane -> Element :*: Element;
        makePage textsel (MkPane rname ui _) = uiPage rname $ ui textsel;
        nui: WholeRef (Action TextRef) -> Element;
        nui textsel = notebook tabRef $ mapList (makePage textsel) panes;
        itemsRef = forWhole panes $ \(MkPane _ _ rmenus) => rmenus;
        in MkPane (immutWhole title) nui $ fromMaybeWhole {index %itemsRef %tabRef};
    end;

selectionPane:
    Text -> # window title
    SetRef item -> # the set of items
    List ((item -> Action Unit) -> MenuItem) -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> Element) ->
    Action Pane;
selectionPane title iset createMenuItems iopen iui = do
    selection <- newMemWhole;
    return $ let
        viewItem: Action Unit;
        viewItem = do
            item <- get selection;
            iopen item;
            return ();
            end;

        deleteItem: Action Unit;
        deleteItem = do
            item <- get selection;
            iset -= item;
            end;

        moremenus: List MenuItem;
        moremenus =
            [
                menuSubmenu "Selection" $ mapList (\mi => mi $ \i => selection := i) createMenuItems ++
                [
                    menuAction "View" Nothing {viewItem},
                    menuAction "Delete" Nothing {deleteItem}
                ]
            ];
        in MkPane {title} (\_ => iui selection) {moremenus};
    end;

selectionPairPane:
    Text -> # pane title
    SetRef item -> # the set of items
    List ((item -> Action Unit) -> MenuItem) -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> Element) -> # Element for the item selector
    (WholeRef +item -> Element) -> # Element for the selected item
    Action Pane;
selectionPairPane title iset createMenuItems iopen getSelectorUI getSelectedUI =
    selectionPane title iset createMenuItems iopen $ \selection => let
        sidebarUI: Element;
        sidebarUI = getSelectorUI selection;

        selectedUI: Element;
        selectedUI = getSelectedUI $ immutWhole selection;

        in horizontal [sidebarUI, layoutGrow selectedUI];

selectionWindow:
    Context ->
    Text -> # window title
    SetRef item -> # the set of items
    List ((item -> Action Unit) -> MenuItem) -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> Element) ->
    Action Window;
selectionWindow gtk wtitle iset createMenuItems iopen iui = do
    mui <- selectionPane wtitle iset createMenuItems iopen iui;
    paneWindow gtk mui;
    end;

#| This is a two-pane window, the left has a "selector" of items (e.g. a table), and the right shows the selected item.
selectionPairWindow:
    Context ->
    Text -> # window title
    SetRef item -> # the set of items
    List ((item -> Action Unit) -> MenuItem) -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> Element) -> # Element for the item selector
    (WholeRef +item -> Element) -> # Element for the selected item
    Action Window;
selectionPairWindow gtk wtitle iset createMenuItems iopen getSelectorUI getSelectedUI = do
    mui <- selectionPairPane wtitle iset createMenuItems iopen getSelectorUI getSelectedUI;
    paneWindow gtk mui;
    end;

newItemMenuAction: Text -> Maybe Text -> Action a -> SetRef a -> (a -> Action Unit) -> MenuItem;
newItemMenuAction name mkey newItem set setsel = menuAction name mkey
    {do
    item <- newItem;
    set += item;
    setsel item;
    end};

in expose
    stdWindow,
    selectionWindow,
    selectionPairWindow,
    uiPage,
    uiLabelled,
    Pane,
    MkPane,
    pane,
    paneWindow,
    notebookPane,
    selectionPane,
    selectionPairPane,
    newItemMenuAction
