let

### Useful UI stuff

uiPage: WholeRef +Text -> UI.Element -> (UI.Element,UI.Element);
uiPage n ui = (UI.label n,ui);

uiLabelled: WholeRef +Text -> UI.Element -> UI.Element;
uiLabelled n ui = UI.horizontal [(False, UI.label n),(True, ui)];

# A window that comes with some menus.
stdWindow: WholeRef +Text -> WholeRef +[UI.MenuItem] -> UI.Element -> Action UI.Window;
stdWindow title moremenus contents = fixAction $ \window -> let
    menus: WholeRef +[UI.MenuItem];
    menus =
        {[
            UI.menuSubmenu "File"
            [
                UI.menuAction "Close" (Just "Ctrl+W") {UI.closeWindow window},
                UI.menuSeparator,
                UI.menuAction "Exit" (Just "Ctrl+Q") {UI.exitUI}
            ],
            UI.menuSubmenu "Edit"
            [
                UI.menuAction "Undo" (Just "Ctrl+Z") {queueUndo},
                UI.menuAction "Redo" (Just "Ctrl+Y") {queueRedo}
            ]
        ] ++ %moremenus};
    in UI.openWindow (600,800) title menus $ UI.scrolled contents;

datatype Pane = MkPane (WholeRef Text) UI.Element (WholeRef [UI.MenuItem]);

paneWindow: Pane -> Action UI.Window;
paneWindow (MkPane title ui moremenus) = stdWindow title moremenus ui;

pane: Text -> UI.Element -> Pane;
pane title ui = MkPane {title} ui {[]};

notebookPane: WholeRef +Text -> [Pane] -> Action Pane;
notebookPane title panes = do
    tabRef <- newMemWhole;
    return $ let
        makePage: Pane -> (UI.Element,UI.Element);
        makePage (MkPane rname ui _) = uiPage rname ui;
        nui: UI.Element;
        nui = UI.notebook tabRef $ mapList makePage panes;
        itemsRef = forWhole panes $ \(MkPane _ _ rmenus) -> rmenus;
        in MkPane (immutWhole title) nui $ fromMaybeWhole {index %itemsRef %tabRef};
    end;

selectionPane:
    Text -> # window title
    SetRef item -> # the set of items
    [(item -> Action ()) -> UI.MenuItem] -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> UI.Element) ->
    Action Pane;
selectionPane title iset createMenuItems iopen iui = do
    selection <- newMemWhole;
    return $ let
        viewItem: Action ();
        viewItem = do
            item <- get selection;
            iopen item;
            return ();
            end;

        deleteItem: Action ();
        deleteItem = do
            item <- get selection;
            iset -= item;
            end;

        moremenus: [UI.MenuItem];
        moremenus =
            [
                UI.menuSubmenu "Selection" $ mapList (\mi -> mi $ \i -> selection := i) createMenuItems ++
                [
                    UI.menuAction "View" Nothing {viewItem},
                    UI.menuAction "Delete" Nothing {deleteItem}
                ]
            ];
        in MkPane {title} (iui selection) {moremenus};
    end;

selectionPairPane:
    Text -> # window title
    SetRef item -> # the set of items
    [(item -> Action ()) -> UI.MenuItem] -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> UI.Element) -> # UI.Element for the item selector
    (WholeRef +item -> UI.Element) -> # UI.Element for the selected item
    Action Pane;
selectionPairPane title iset createMenuItems iopen getSelectorUI getSelectedUI =
    selectionPane title iset createMenuItems iopen $ \selection -> let
        sidebarUI: UI.Element;
        sidebarUI = getSelectorUI selection;

        selectedUI: UI.Element;
        selectedUI = getSelectedUI $ immutWhole selection;

        in UI.horizontal [(False,sidebarUI),(True,selectedUI)];

selectionWindow:
    Text -> # window title
    SetRef item -> # the set of items
    [(item -> Action ()) -> UI.MenuItem] -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> UI.Element) ->
    Action UI.Window;
selectionWindow wtitle iset createMenuItems iopen iui = do
    mui <- selectionPane wtitle iset createMenuItems iopen iui;
    paneWindow mui;
    end;

# This is a two-pane window, the left has a "selector" of items (e.g. a table), and the right shows the selected item.
selectionPairWindow:
    Text -> # window title
    SetRef item -> # the set of items
    [(item -> Action ()) -> UI.MenuItem] -> # Menu items for creating new items
    (item -> Action Any) -> # what to do when an item is activated
    (WholeRef item -> UI.Element) -> # UI.Element for the item selector
    (WholeRef +item -> UI.Element) -> # UI.Element for the selected item
    Action UI.Window;
selectionPairWindow wtitle iset createMenuItems iopen getSelectorUI getSelectedUI = do
    mui <- selectionPairPane wtitle iset createMenuItems iopen getSelectorUI getSelectedUI;
    paneWindow mui;
    end;

newItemMenuAction: Text -> Maybe Text -> Action a -> SetRef a -> (a -> Action ()) -> UI.MenuItem;
newItemMenuAction name mkey newItem set setsel = UI.menuAction name mkey
    {do
    item <- newItem;
    set += item;
    setsel item;
    end};

in expose
    stdWindow
    selectionWindow
    selectionPairWindow
    uiPage
    uiLabelled
    Pane
    MkPane
    pane
    paneWindow
    notebookPane
    selectionPane
    selectionPairPane
    newItemMenuAction
